%include {
#include <stdlib.h>
#include <string.h>
#include <assert.h>

#include "parse.h"
#include "snc_main.h"
}

%extra_argument { Program **presult }

%name parser

%parse_failure {
	report("parser giving up\n");
	exit(1);
}

%syntax_error {
	report_at(TOKEN.file, TOKEN.line,
        	"syntax error near token '%s'\n", TOKEN.str);
	exit(1);
}

%token_type { Token }
%default_type { Expr* }

/* Standard C operator table
  Primary Expression Operators  () [] . -> expr++ expr--  left-to-right
  Unary Operators  * & + - ! ~ ++expr --expr (typecast) sizeof()  right-to-left
  Binary Operators  * / %  left-to-right
  + -
  >> <<
  < > <= >=
  == !=
  &
  ^
  |
  &&
  ||
  Ternary Operator  ?:  right-to-left
  Assignment Operators  = += -= *= /= %= >>= <<= &= ^= |=  right-to-left
  Comma  ,  left-to-right
*/

// PRE and POST are pseudo tokens, they only for the
// precedence declaration.

// We do not support the comma operator, except
// in for(;;), where it is built-in.
// %left	COMMA.

%right	EQUAL ADDEQ SUBEQ ANDEQ OREQ
	DIVEQ MULEQ MODEQ LSHEQ RSHEQ XOREQ.
%right	QUESTION COLON.
%left	OROR.
%left	ANDAND.
%left	VBAR.
%left	CARET.
%left	AMPERSAND.
%left	EQ NE.
%left	GT GE LE LT.
%left	LSHIFT RSHIFT.
%left	ADD SUB.
%left	ASTERISK SLASH MOD.
%right	NOT INCR DECR PRE.
%left	LBRACKET RBRACKET POINTER PERIOD POST.
// LPAREN RPAREN not listed as we do not support indirect calls. */

program ::=
	program_name(pn)
	program_param(pp)
	definitions(ds)
	global_entry_code(en)
	state_sets(ss)
	global_exit_code(ex)
	c_codes(cc).
{
	*presult = program(pn,pp,ds,en,ss,ex,cc);
}

%type program_name {char*}
program_name(p) ::= PROGRAM NAME(t).	{ p = t.str; }

%type program_param {char*}
program_param(p) ::=
	LPAREN STRCON(t) RPAREN.	{ p = t.str; }
program_param(p) ::= .			{ p = ""; }

definitions(p) ::= definitions(xs) definition(x). {
	p = link_expr(xs, x);
}
definitions(p) ::= definition(x).	{ p = x; }

definition(p) ::= assign(x).		{ p = x; }
definition(p) ::= monitor(x).		{ p = x; }
definition(p) ::= sync(x).		{ p = x; }
definition(p) ::= syncq(x).		{ p = x; }
definition(p) ::= decl(x).		{ p = x; }
definition(p) ::= option(x).		{ p = x; }
definition(p) ::= c_code(x).		{ p = x; }

assign(p) ::= ASSIGN NAME(v) to string(t) SEMICOLON. {
	p = expr(E_ASSIGN, v, 0, t);
}
assign(p) ::= ASSIGN NAME(v) subscript(s) to string(t) SEMICOLON. {
	p = expr(E_ASSIGN, v, expr(E_CONST, s, 0, 0), t);
}
assign(p) ::= ASSIGN NAME(v) to LBRACE strings(ss) RBRACE SEMICOLON. {
	p = expr(E_ASSIGN, v, 0, ss);
}

strings(p) ::= strings(xs) COMMA string(x). {
	p = link_expr(xs, x);
}
strings(p) ::= string(x). {
	p = x;
}

monitor(p) ::= MONITOR NAME(v) opt_subscript(s) SEMICOLON. {
	p = expr(E_MONITOR, v, s, 0);
}

sync(p) ::= SYNC NAME(v) opt_subscript(s) to NAME(ev) SEMICOLON. {
	p = expr(E_SYNC, v, s, expr(E_X, ev, 0, 0));
}

syncq(p) ::= SYNCQ NAME(v) opt_subscript(s) to NAME(ev) syncq_size(n) SEMICOLON. {
	p = expr(E_SYNCQ, v, s, expr(E_X, ev, n, 0));
}

syncq_size(p) ::= INTCON(n).		{ p = expr(E_CONST, n, 0, 0); }
syncq_size(p) ::= .			{ p = 0; }

to ::= TO.
to ::= .

opt_subscript(p) ::= subscript(s).	{ p = expr(E_CONST, s, 0, 0); }
opt_subscript(p) ::= .			{ p = 0; }

%type subscript {Token}
subscript(p) ::= LBRACKET INTCON(n) RBRACKET. { p = n; }

decl(p) ::= type(t) NAME(v) SEMICOLON.
		{ p = decl(t, VC_SIMPLE,  v, 0,  0,  0); }
decl(p) ::= type(t) NAME(v) EQUAL number(n) SEMICOLON.
		{ p = decl(t, VC_SIMPLE,  v, 0,  0, n.str); }
decl(p) ::= type(t) NAME(v) subscript(s) SEMICOLON.
		{ p = decl(t, VC_ARRAY1,  v, s.str,  0,  0); }
decl(p) ::= type(t) NAME(v) subscript(s1) subscript(s2) SEMICOLON.
		{ p = decl(t, VC_ARRAY2,  v, s1.str, s2.str, 0); }
decl(p) ::= type(t) ASTERISK NAME(v) SEMICOLON.
		{ p = decl(t, VC_POINTER, v, 0, 0, 0); }
decl(p) ::= type(t) ASTERISK NAME(v) subscript(s) SEMICOLON.
		{ p = decl(t, VC_ARRAYP,  v, s.str,  0,  0); }

%type number {Token}
number(p) ::= INTCON(x).			{ p = x; }
number(p) ::= FPCON(x).				{ p = x; }

%type type {int}
type(p) ::= CHAR.				{ p = V_CHAR;	}
type(p) ::= SHORT.				{ p = V_SHORT;	}
type(p) ::= INT.				{ p = V_INT;	}
type(p) ::= LONG.				{ p = V_LONG;	}
type(p) ::= UNSIGNED CHAR.			{ p = V_UCHAR;	}
type(p) ::= UNSIGNED SHORT.			{ p = V_USHORT;	}
type(p) ::= UNSIGNED INT.			{ p = V_UINT;	}
type(p) ::= UNSIGNED LONG.			{ p = V_ULONG;	}
type(p) ::= FLOAT.				{ p = V_FLOAT;	}
type(p) ::= DOUBLE.				{ p = V_DOUBLE;	}
type(p) ::= STRING.				{ p = V_STRING;	}
type(p) ::= EVFLAG.				{ p = V_EVFLAG;	}

option(p) ::= OPTION option_value(v) NAME(n) SEMICOLON.
						{ p = expr(E_OPTION, n, v, 0); }

option_value(p) ::= ADD(t).			{ p = expr(E_X, t, 0, 0); }
option_value(p) ::= SUB(t).			{ p = expr(E_X, t, 0, 0); }

// State sets and states

global_entry_code(p) ::= ENTRY block(xs).	{ p = xs; }
global_entry_code(p) ::= .			{ p = 0; }

global_exit_code(p) ::= EXIT block(xs).		{ p = xs; }
global_exit_code(p) ::= .			{ p = 0; }

state_sets(p) ::= state_sets(xs) state_set(x).	{ p = link_expr(xs, x); }
state_sets(p) ::= state_set(x).			{ p = x; }

state_set(p) ::= SS NAME(n) LBRACE states(xs) RBRACE. {
	p = expr(E_SS, n, xs, 0);
}

states(p) ::= states(xs) state(x).		{ p = link_expr(xs, x); }
states(p) ::= state(x).				{ p = x; }

state(p) ::= STATE NAME(n) LBRACE state_definitions(ds) state_blocks(xs) RBRACE.
						{ p = expr(E_STATE, n, xs, ds); }

state_definitions(p) ::= state_definitions(xs) state_definition(x). {
	p = link_expr(xs, x);
}
state_definitions(p) ::= .			{ p = 0; }

// state_definition(p) ::= assign(x).		{ p = x; }
// state_definition(p) ::= monitor(x).		{ p = x; }
// state_definition(p) ::= sync(x).		{ p = x; }
// state_definition(p) ::= syncq(x).		{ p = x; }
// state_definition(p) ::= decl(x).		{ p = x; }
state_definition(p) ::= option(x).		{ p = x; }
state_definition(p) ::= c_code(x).		{ p = x; }

state_blocks(p) ::= entries(xs) transitions(ts) exits(ys).
						{ p = link_expr(link_expr(xs, ts), ys); }

entries(p) ::= entries(xs) entry(x).		{ p = link_expr(xs, x); }
entries(p) ::= .				{ p = 0; }

entry(p) ::= ENTRY(t) block(xs).		{ p = expr(E_ENTRY, t, 0, xs); }

exits(p) ::= exits(xs) exit(x).			{ p = link_expr(xs, x); }
exits(p) ::= .					{ p = 0; }

exit(p) ::= EXIT(t) block(xs).			{ p = expr(E_EXIT, t, 0, xs); }

transitions(p) ::= transitions(xs) transition(x). { p = link_expr(xs, x); }
transitions(p) ::= transition(x).		{ p = x; }

transition(p) ::= WHEN LPAREN opt_expr(c) RPAREN block(xs) STATE NAME(n).
						{ p = expr(E_WHEN, n, c, xs); }

block(p) ::= LBRACE statements(xs) RBRACE.	{ p = xs; }

// Statements

statements(p) ::= statements(xs) statement(x).	{ p = link_expr(xs, x); }
statements(p) ::= .				{ p = 0; }

statement(p) ::= BREAK(t) SEMICOLON.		{ p = expr(E_BREAK, t, 0, 0); }
statement(p) ::= c_code(x).			{ p = x; }
statement(p) ::= LBRACE(t) statements(xs) RBRACE. { p = expr(E_CMPND, t, xs, 0); }
statement(p) ::= IF(t) LPAREN expr(c) RPAREN statement(x).
						{ p = expr(E_IF, t, c, x); }
statement(p) ::= ELSE(t) statement(x).		{ p = expr(E_ELSE, t, x, 0); }
statement(p) ::= WHILE(t) LPAREN expr(c) RPAREN statement(x).
						{ p = expr(E_WHILE, t, c, x); }
statement(p) ::= for_statement(x).		{ p = x; }
statement(p) ::= opt_expr(x) SEMICOLON(t).	{ p = expr(E_STMT, t, x, 0); }

for_statement(p) ::=
	FOR(for) LPAREN
		exprs(init) SEMICOLON(sem1) opt_expr(cond) SEMICOLON(sem2) exprs(iter)
	RPAREN statement(x).			{ p = expr(E_FOR, for,
							expr(E_X, sem1, init, cond),
							expr(E_X, sem2, iter, x));
						}

// Expressions

// Atomic
expr(p) ::= number(x).				{ p = expr(E_CONST, x, 0, 0); }
expr(p) ::= string(x).				{ p = x; }
expr(p) ::= NAME(v).				{ p = expr(E_VAR, v, 0, 0); }

// Parenthesized
expr(p) ::= LPAREN(t) expr(x) RPAREN.		{ p = expr(E_PAREN, t, x, 0); }

// Primary Expression and Unary Postfix Operators
expr(p) ::= NAME(t) LPAREN exprs(xs) RPAREN. [POST]
						{ p = expr(E_FUNC, t, xs, 0); }
expr(p) ::= EXIT(t) LPAREN exprs(xs) RPAREN. [POST] { p = expr(E_FUNC, t, xs, 0); }
expr(p) ::= expr(x) LBRACKET(t) expr(y) RBRACKET. [POST]
						{ p = expr(E_SUBSCR, t, x, y); }
expr(p) ::= expr(x) PERIOD(t)  expr(y).	[POST]	{ p = expr(E_BINOP,  t, x, y); }
expr(p) ::= expr(x) POINTER(t) expr(y).	[POST]	{ p = expr(E_BINOP,  t, x, y); }
expr(p) ::= expr(x) INCR(t).		[POST]	{ p = expr(E_POST,   t, x, 0); }
expr(p) ::= expr(x) DECR(t).		[POST]	{ p = expr(E_POST,   t, x, 0); }

// Unary Prefix Operators
expr(p) ::= ADD(t)	expr(x). [PRE]		{ p = expr(E_PRE, t, x, 0); }
expr(p) ::= SUB(t)	expr(x). [PRE]		{ p = expr(E_PRE, t, x, 0); }
expr(p) ::= ASTERISK(t)	expr(x). [PRE]		{ p = expr(E_PRE, t, x, 0); }
expr(p) ::= AMPERSAND(t)expr(x). [PRE]		{ p = expr(E_PRE, t, x, 0); }
expr(p) ::= NOT(t)	expr(x). [PRE]		{ p = expr(E_PRE, t, x, 0); }
expr(p) ::= TILDE(t)	expr(x). [PRE]		{ p = expr(E_PRE, t, x, 0); }
expr(p) ::= INCR(t)	expr(x). [PRE]		{ p = expr(E_PRE, t, x, 0); }
expr(p) ::= DECR(t)	expr(x). [PRE]		{ p = expr(E_PRE, t, x, 0); }

// Binary Operators, left-to-right
expr(p) ::= expr(x) SUB(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) ADD(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) ASTERISK(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) SLASH(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) GT(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) GE(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) EQ(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) NE(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) LE(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) LT(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) OROR(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) ANDAND(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) LSHIFT(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) RSHIFT(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) VBAR(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) CARET(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) AMPERSAND(t) expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) MOD(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }

// Ternary Operator, right-to-left
expr(p) ::= expr(x) QUESTION(t1) expr(y) COLON(t2) expr(z). {
	p = expr(E_TERNOP, t1, x, expr(E_X, t2, y, z));
}

// Assignment Operators, right-to-left
expr(p) ::= expr(x) EQUAL(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) ADDEQ(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) SUBEQ(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) ANDEQ(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) OREQ(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) DIVEQ(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) MULEQ(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) MODEQ(t) 	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) LSHEQ(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) RSHEQ(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }
expr(p) ::= expr(x) XOREQ(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }

// Comma, left-to-right, not supported
// expr(p) ::= expr(x) COMMA(t)	expr(y).	{ p = expr(E_BINOP, t, x, y); }

opt_expr(p) ::= expr(x).			{ p = x; }
opt_expr(p) ::= .				{ p = 0; }

exprs(p) ::= exprs(xs) COMMA expr(x).		{ p = link_expr(xs, x); }
exprs(p) ::= expr(x).				{ p = x; }
exprs(p) ::= .					{ p = 0; }

string(p) ::= STRCON(t).			{ p = expr(E_STRING, t, 0, 0); }

// Literal (C) code

c_codes(p) ::= c_codes(xs) c_code(x).		{ p = link_expr(xs, x); }
c_codes(p) ::= .				{ p = 0; }

c_code(p) ::= CCODE(t). 			{ p = expr(E_TEXT, t, 0, 0); }
